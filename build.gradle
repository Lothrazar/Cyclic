buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "${mc_version}-${mod_version}"
group = "com.lothrazar.cyclicmagic" 
archivesBaseName = "Cyclic"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {

    version = "${mc_version}-${forge_version}"
    mappings = project.mappings_version
    runDir = "run"
    makeObfSourceJar = false
    useDepAts = true
    //Makes those replacement changes to the main mod class.

    replace '@VERSION@', project.version
    //Replaces all text that matches the left side with the right.
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
    //Makes those replacement changes to the main mod class.
    //My mod class is set in my project's gradle.properties file.
    replaceIn "ModCyclic.java"
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing. 
    // Using project.propName would cause the script to fail validation if 
    // the property did not exist. 
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

//Defines basic patterns for pulling various dependencies.
repositories {
    maven { url 'https://www.cursemaven.com'  }
    maven { url "https://dvs1.progwml6.com/files/maven"    }
    maven { url "https://modmaven.k-4u.nl"  }
    maven { url "https://maven.theillusivec4.top/"    }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://maven.blamejared.com/vazkii" }
    maven { url "http://minecraft.curseforge.com/api/maven/"    }
    maven { url 'https://www.cursemaven.com'  }
    maven { url "https://maven.thiakil.com/"    } 
	maven {	url "https://maven.mcmoddev.com"	}

   // maven { //Guide API
	//	url "http://tehnut.info/maven/"
	//}

	maven { url "http://maven.amadornes.com/" }
	ivy {
		name 'asie dependency mirror'
		artifactPattern "http://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
	}
}

dependencies {
  //if any dependencies fail to fetch, download and put in ./libs/
  deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
  deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:${crt_version}"
  deobfCompile "baubles:Baubles:1.12:${baubles_version}"
  deobfCompile "fastworkbench:FastWorkbench:${mc_version}:${fb_version}"
  //TODO: fix guide api gradle, or else ./libs/Guide-API-1.12-2.1.8-63.jar 
  deobfCompile "info.amerifrance.guideapi:Guide-API:${guideapi_version}"
  deobfCompile  "vazkii.patchouli:Patchouli:${patchouli_version}"

  // https://maven.mcmoddev.com/net/darkhax/enchdesc/EnchantmentDescriptions-1.12.2/1.1.9/
  // deobfCompile "net.darkhax.enchdesc:EnchantmentDescriptions-${mc_version}-${ench_descr_version}:+"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


build.dependsOn signJar

task copyJar(type: Copy) {
    from jar
    into "${dist_folder}"
}

build.finalizedBy copyJar

cleanJar {
  delete 'build/libs'
}
